apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'cpp-lib'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:1.8.7'
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
}

mainClassName = 'net.rubygrapefruit.platform.Main'
def nativeHeadersDir = file("$buildDir/nativeHeaders")

libraries {
    main {
        spec {
            includes([nativeHeadersDir])
            if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
                includes(['/System/Library/Frameworks/JavaVM.framework/Versions/Current/Headers/'])
            } else {
                includes(["${org.gradle.internal.jvm.Jvm.current().javaHome}/include"])
                includes(["${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"])
            }
        }
    }
}

task nativeHeaders {
    def outputFile = file("$nativeHeadersDir/native.h")
    inputs.files sourceSets.main.output
    outputs.file outputFile
    doLast {
        outputFile.parentFile.mkdirs()
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('javah')
            args '-o', outputFile
            args '-classpath', sourceSets.main.output.classesDir
            args 'net.rubygrapefruit.platform.internal.PosixFileFunctions'
            args 'net.rubygrapefruit.platform.internal.PosixProcessFunctions'
            args 'net.rubygrapefruit.platform.internal.PosixTerminalFunctions'
        }
    }
}

compileMain.dependsOn nativeHeaders
test.dependsOn compileMain

